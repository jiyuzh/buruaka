#!/usr/bin/env perl

use warnings;
use strict;

use File::Basename;
use Data::Dumper;
use Getopt::Long;
use Pod::Usage;

my $SCRIPT_DIR = dirname(__FILE__);

sub help
{
	my ($exitval) = @_;

	my $prog = basename($0);

	my $help = <<"EOF";
grub-next v1.0.0
Jiyuan Zhang <jiyuanz3\@illinois.edu>

grub-next is a wrapper around grub-reboot which let you filter desired boot
entries and set is as the target of next boot.

USAGE:
    $prog [OPTIONS] PATTERN [--list]
    $prog [OPTIONS] --current
    $prog [OPTIONS] --list

ARGS:
    <PATTERN>
        A regular expression used for searching. To match a pattern beginning
        with a dash, use the special '--' delimiter to indicate that no more
        flags will be provided.

OPTIONS:
    -c, --current
        Display the active boot entry and quit.

    -d, --dryrun
        Run the program without changing system setting.

    -g, --grub, --grubpath <PATH>
        Specify the grub environment folder to work with.

        The default value is: /boot/grub/

    -h, -?, --help
        Show this help.

    -i, --index, --offset <INDEX>
        When multiple bootable entries matches the pattern, the entry listed at
        the specified index will be selected.

        The default value is: 0

    -l, --list
        List all bootable entries.

    -p, --persistent
        The boot entry choice shall be preserved across multiple reboot.

    -s, --show
        Display the content of the selected boot entry and quit without changing
        system settings.

    -v, -vv
        Change the verbosity of the program output.

OUTPUT:
    Status messages for human consumption.

EXIT STATUS:
    0   The requested action was successfully performed.

    1   Invalid command-line usage.

    2   Given boot entry is not found.

    3   System setting change failed.

EOF
	print($help);

	if ($exitval =~ /^\d+$/) {
		exit $exitval;
	}

	exit 0;
}

sub parseGrubCfg
{
	my ($verbose, $path) = @_;

	my @fqnstack;
	my @namstack;
	my @posstack;
	my @result;
	my $cursor = "ERR_SUBMENU_NULL";
	my $submenu = "! Unknown submenu entry !";
	my $offset = 0;

	$path = $path . "/grub.cfg";

	print("Parsing GRUB config file: $path") if ($verbose >= 1);

	open my $grub_fh, $path or die "Could not open $path: $!\n";
	my $grub_text = do { local $/; <$grub_fh> };
	close($grub_fh);

	my $prev = 0;

	while ($grub_text =~ /^\h*(?P<class>menuentry|submenu)\h+'(?P<name>[^']+)'\h+[^']+\h+'(?P<id>[^']+)'\h/gm) {
		my $class = $+{class};
		my $name = $+{name};
		my $id = $+{id};

		# This is to support submenu nesting and optionally detect syntax errors
		my $prefix = substr($grub_text, $prev, $-[0] - $prev);
		$prev = $+[0];

		foreach my $char (split('', $prefix)) {
			if ($char eq '{') {
				push(@fqnstack, $cursor);
				$cursor = "ERR_SUBMENU_NULL";
				push(@namstack, $submenu);
				$submenu = "! Unknown submenu entry !";
				push(@posstack, $offset);
				$offset = 0;
			}
			if ($char eq '}') {
				$cursor = pop(@fqnstack);
				$submenu = pop(@namstack);
				$offset = pop(@posstack);
			}
		}

		# Submenu, setup stack frame and parse next
		if ($class eq "submenu") {
			$cursor = $id;
			$submenu = $name;
			next;
		}

		# Menuentry, dump stack as a result
		if ($class eq "menuentry") {
			my @fqns = @fqnstack;
			push(@fqns, $id);
			my $fqn = join('>', @fqns);

			my @nams = @namstack;
			push(@nams, $name);
			my $nam = join(' -> ', @nams);

			my @poss = @posstack;
			push(@poss, $offset);
			my $pos = join('>', @poss);

			# We also keep the start position so we can extract content
			my %entry = (
				"name" => $nam,
				"fqn" => $fqn,
				"pos" => $pos,
				"text_start" => $-[0]);
			push(@result, \%entry);
		}

		# TODO: This may be buggy with consecutive submenu
		$offset++;
	}

	print(" -> Loaded " . scalar(@result) . " bootable entries\n") if ($verbose >= 1);

	return @result;
}

sub strGrubEntry
{
	my ($entry) = @_;
	return "[$entry->{pos}] $entry->{name}";
}

sub listGrubEntry
{
	my ($verbose, @entries) = @_;
	my $idx = 0;

	print("\n") if ($verbose >= 1);

	foreach my $entry (@entries) {
		print("$idx. " . strGrubEntry($entry) . "\n");
		$idx++;
	}
}

sub filterGrubEntry
{
	my ($verbose, $pattern, @entries) = @_;

	print("Filtering entries with PCRE pattern: /$pattern/gm") if ($verbose >= 1);

	my @result;

	foreach my $entry (@entries) {
		# Only filter on human_name and id_expr as they are stable
		if ($entry->{name} =~ /$pattern/gm || $entry->{fqn} =~ /$pattern/gm) {
			push(@result, $entry);
		}
	}

	print(" -> Gathered " . scalar(@result) . " bootable entries.\n") if ($verbose >= 1);

	listGrubEntry(@result) if ($verbose >= 2);

	return @result;
}

sub showGrubBoot
{
	my ($verbose, $path, $entry) = @_;

	my $balance = 0;

	$path = $path . "/grub.cfg";

	open my $grub_fh, $path or die "Could not open $path: $!\n";
	my $grub_text = do { local $/; <$grub_fh> };
	close($grub_fh);

	my $text = "";

	# Take every thing before first '{' (entry header) and the paired '}' (entry body)
	foreach my $char (split('', substr($grub_text, $entry->{text_start}))) {
		$text .= $char;

		if ($char eq '{') {
			$balance++;
		}
		if ($char eq '}') {
			$balance--;
			if ($balance <= 0) {
				last;
			}
		}
	}

	print($text);
}

sub selectGrubBoot
{
	my ($verbose, $path, $persistent, $dryrun, $entry) = @_;

	my $fqn = $entry->{fqn};
	my $output = "";
	my $ret = 0;

	# Dryrun, we may use `true` to call the real routine but it will trigger post-validation error
	if ($dryrun != 0) {
		print("\n") if ($verbose >= 1);
		print("Dry run boot entry is: " . strGrubEntry($entry) . "\n");

		my $real = probeGrubEntry($verbose, $path);
		print("Current active boot entry is: $real\n");
		return 0;
	}

	if ($persistent == 0) {
		$output = `grub-reboot '$fqn' 2>&1`;
	}
	else {
		# NOTE: grub-set-default will erase grub-reboot config
		$output = `grub-set-default '$fqn' 2>&1`;
	}

	$ret = $?;

	my $real = probeGrubEntry($verbose, $path);

	if ($ret == 0 && $output eq "") {
		print("\n") if ($verbose >= 1);

		if ($real eq $fqn) {
			print("Set boot entry to: " . strGrubEntry($entry) . "\n");
			print("Current active boot entry is: $real\n");
			return 0;
		}
		else {
			print("Unable to set boot entry to: " . strGrubEntry($entry) . "\n");
			print("Current active boot entry is: $real\n");
			return 3;
		}
	}
	else {
		print("\n") if ($verbose >= 1);
		print($output);
		print("\n");
		print("Failed to set boot entry to: " . strGrubEntry($entry) . "\n");
		print("Current active boot entry is: $real\n");
		return 3;
	}
}

sub probeGrubEntry
{
	my ($verbose, $path) = @_;

	$path = $path . "/grubenv";

	print("Checking GRUB environment file: $path") if ($verbose >= 1);

	open my $grub_fh, $path or die "Could not open $path: $!\n";
	my $grub_text = do { local $/; <$grub_fh> };
	close($grub_fh);

	# This fallback order is from GRUB source code
	if ($grub_text =~ /^\h*next_entry\h*=\h*(.+)\h*$/gm) {
		return $1;
	}

	if ($grub_text =~ /^\h*saved_entry\h*=\h*(.+)\h*$/gm) {
		return $1;
	}

	return "! Unknown boot entry !";
}

GetOptions(
	'help|?' => \&help,

	'current!' => \(my $current = 0),
	'dryrun!' => \(my $dryrun = 0),
	'grubpath=s' => \(my $grub = "/boot/grub/"),
	'index|offset=i' => \(my $index = 0),
	'list!' => \(my $listmode = 0),
	'persistent!' => \(my $persistent = 0),
	'show!' => \(my $detail = 0),
	'verbose+' => \(my $verbose = 0),
) or help(1);

# Current mode, just show the current entry and quit
if ($current == 1) {
	my $real = probeGrubEntry($verbose, $grub);
	print("Current active boot entry is: $real\n");
	exit 0;
}

# The only positional we accept is a pattern
if (scalar(@ARGV) > 1) {
	help(1);
}

# Positional-less listing mode is allowed (list all parsed entries)
if (!$listmode && scalar(@ARGV) < 1) {
	help(1);
}

my @all = parseGrubCfg($verbose, $grub);

# List all parsed entries if asked
if ($listmode != 0 && scalar(@ARGV) < 1) {
	listGrubEntry($verbose, @all);
	exit 0;
}

my $pattern = $ARGV[0];
my @result = filterGrubEntry($verbose, $pattern, @all);

# List matched entries if asked
if ($listmode != 0) {
	listGrubEntry($verbose, @result);
	exit 0;
}

# Pick the choosen one
if ($index >= scalar(@result)) {
	print("ERROR: Invalid index value $index\n");
	exit 2;
}

print("Selected $index-th boot entry\n") if ($verbose >= 1);

my $ret = 0;

# See if the user want to see the entry content or apply it
if ($detail) {
	showGrubBoot($verbose, $grub, $result[$index]);
}
else {
	$ret = selectGrubBoot($verbose, $grub, $persistent, $dryrun, $result[$index]);
}

exit $ret;
